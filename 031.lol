HAI 1.3

    I HAS A COINZ ITZ A BUKKIT
    COINZ HAS A SRS 0 ITZ 200
    COINZ HAS A SRS 1 ITZ 100
    COINZ HAS A SRS 2 ITZ 50
    COINZ HAS A SRS 3 ITZ 20
    COINZ HAS A SRS 4 ITZ 10
    COINZ HAS A SRS 5 ITZ 5
    COINZ HAS A SRS 6 ITZ 2
    COINZ HAS A SRS 7 ITZ 1

    BTW MEMORY[R][C] ITZ THE NUMBR OF WAYS YOU CAN MAKE C USING COINZ <= R
    I HAS A MEMORY ITZ A BUKKIT
    IM IN YR LOOP UPPIN YR ROW WILE DIFFRINT ROW 8
        MEMORY HAS A SRS ROW ITZ A BUKKIT
        IM IN YR LOOOP UPPIN YR COL WILE DIFFRINT COL 201
            MEMORY'Z SRS ROW HAS A SRS COL ITZ 0
        IM OUTTA YR LOOOP
    IM OUTTA YR LOOP

    I HAS A WAYS
    I HAS A COINVALUE
    IM IN YR LOOP UPPIN YR ROW WILE DIFFRINT ROW 8
        IM IN YR LOOOP UPPIN YR COL WILE DIFFRINT COL 201
            WAYS R 0
            IM IN YR LOOOOP UPPIN YR COIN WILE DIFFRINT COIN SUM OF ROW 1
                COINVALUE R COINZ'Z SRS COIN
                BOTH SAEM COINVALUE COL, O RLY?
                YA RLY
                    WAYS R SUM OF WAYS 1
                MEBBE BOTH SAEM BIGGR OF COINVALUE COL COL
                    WAYS R SUM OF...
                               WAYS...
                               MEMORY'Z SRS SUM OF COIN 0'Z SRS DIFF OF COL COINVALUE
                OIC
            IM OUTTA YR LOOOOP
            MEMORY'Z SRS SUM OF ROW 0'Z SRS SUM OF COL 0 R WAYS
        IM OUTTA YR LOOOP
    IM OUTTA YR LOOP

    VISIBLE MEMORY'Z SRS 7'Z SRS 200

KTHXBYE
