HAI 1.2

    BTW THIS ISN'T EASY WITHOUT ARRAYS
    I HAS A LIMIT ITZ 20

    HOW IZ I ISAMAZONPRIME YR AMAZONPRIMEWANNABE
        I HAS A N ITZ 2
        IM IN YR WAREHOUSE
            DIFFRINT...
                BIGGR OF PRODUKT OF N N AMAZONPRIMEWANNABE...
                AMAZONPRIMEWANNABE
            O RLY?, YA RLY, FOUND YR WIN, OIC
            BOTH SAEM MOD OF AMAZONPRIMEWANNABE N 0
            O RLY?, YA RLY, FOUND YR FAIL, OIC
            N R SUM OF N 1
        IM OUTTA YR WAREHOUSE
    IF U SAY SO

    HOW IZ I HOWMANYTIMEZ YR COMPOST AN YR AMAZONPRIME
        OBTW
            THIS GIVETH THE POWA OF THE PRIME FAKTOR AMAZONPRIME IN THE
            FAKTORIZATION OF COMPOST
        TLDR
        I HAS A POWA ITZ 0
        BOTH SAEM COMPOST 0, O RLY?, YA RLY, FOUND YR POWA, OIC
        IM IN YR TEAM
            BOTH SAEM MOD OF COMPOST AMAZONPRIME 0, O RLY?
            YA RLY
                POWA R SUM OF POWA 1
                COMPOST R QUOSHUNT OF COMPOST AMAZONPRIME
            NO WAI
                FOUND YR POWA
            OIC
        IM OUTTA YR TEAM
    IF U SAY SO

    I HAS A SOLUTION ITZ 1
    IM IN YR VAGINA UPPIN YR AMAZONPRIME TIL BOTH SAEM AMAZONPRIME LIMIT
        BTW NOW I CHECK IF IT REALLY IS AN AMAZONPRIME OR JUST A FAKE
        ALL OF...
            I IZ ISAMAZONPRIME YR AMAZONPRIME MKAY...
            DIFFRINT AMAZONPRIME 0...
            DIFFRINT AMAZONPRIME 1...
        MKAY
        O RLY?
        YA RLY
            I HAS A MAXXX ITZ 0
            IM IN YR UTERUS UPPIN YR N TIL BOTH SAEM N LIMIT
                MAXXX R BIGGR OF...
                    MAXXX...
                    I IZ HOWMANYTIMEZ YR N AN YR AMAZONPRIME MKAY
            IM OUTTA YR UTERUS
            IM IN YR CLAN UPPIN YR INDEX WILE DIFFRINT INDEX MAXXX
                SOLUTION R PRODUKT OF SOLUTION AMAZONPRIME
            IM OUTTA YR CLAN
        OIC
    IM OUTTA YR VAGINA
    VISIBLE SOLUTION

KTHXBYE
