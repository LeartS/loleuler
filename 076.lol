HAI 1.3

    BTW MEMORY[R][C] ITZ THE NUMBR OF WAYS YOU CAN MAKE R USING INTS <= C
    I HAS A MEMORY ITZ A BUKKIT
    IM IN YR LOOP UPPIN YR TOTAL WILE DIFFRINT TOTAL 101
        MEMORY HAS A SRS TOTAL ITZ A BUKKIT
        IM IN YR LOOOP UPPIN YR MAXINT WILE DIFFRINT MAXINT 101
            MEMORY'Z SRS TOTAL HAS A SRS MAXINT ITZ 0
        IM OUTTA YR LOOOP
    IM OUTTA YR LOOP

    I HAS A WAYS
    I HAS A MAXINT
    I HAS A INT
    IM IN YR LOOP UPPIN YR TOTAL WILE DIFFRINT TOTAL 101
        MAXINT R 0

        IM IN YR LOOOP
            BOTH SAEM MAXINT SUM OF TOTAL 1, O RLY?, YA RLY, GTFO, OIC
            WAYS R 0
            INT R 0
            IM IN YR LOOOOP
                INT R SUM OF INT 1
                BOTH SAEM INT SUM OF MAXINT 1, O RLY?, YA RLY, GTFO, OIC
                BOTH SAEM INT TOTAL, O RLY?
                YA RLY
                    WAYS R SUM OF WAYS 1
                NO WAI
                    WAYS R SUM OF...
                        WAYS...
                        MEMORY'Z SRS DIFF OF TOTAL SUM OF INT 0'Z SRS INT
                OIC
            IM OUTTA YR LOOOOP
            MEMORY'Z SRS SUM OF TOTAL 0'Z SRS SUM OF MAXINT 0 R WAYS
            MAXINT R SUM OF MAXINT 1
        IM OUTTA YR LOOOP

        BTW FOR MAXINT IN (TOTAL, 101):
        BTW MEMORY[TOTAL][MAXINT] = MEMORY[TOTAL][TOTAL]
        IM IN YR LOOOP
            BOTH SAEM MAXINT 101, O RLY?, YA RLY, GTFO, OIC
            MEMORY'Z SRS SUM OF TOTAL 0'Z SRS SUM OF MAXINT 0 R WAYS
            MAXINT R SUM OF MAXINT 1
        IM OUTTA YR LOOOP

    IM OUTTA YR LOOP

    VISIBLE MEMORY'Z SRS 100'Z SRS 99

KTHXBYE
